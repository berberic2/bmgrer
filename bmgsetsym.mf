%% file: BMGSYM.MF                            
%%                                             
%% Metafont-Definitionen            
%% spezieller  Zeichen             
%%
%%  $Revision: 1.1 $
%%      $Date: 2000/01/28 10:46:25 $
%%   $RCSfile: bmgsetsym.mf,v $
%%		                                             
%% (c)  Robert Eckardt  3.6.1990        
%% Email: roberte@alpha.tp2.ruhr-uni-bochum.de 
%%                                             
%% improved 6.5.1994               
%% (c) M G Berberich                
%% Email: tex@forwiss.uni-passau.de        
%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "Haekchen";
beginchar(oct"031",cap_height#+2u#,cap_height#,0);
adjust_fit(0,0);
pickup pencircle scaled 3rule_thickness;
 lft x1 = u;		    y1 = x_height;
 rt  x3 = w-u;		    y3 = cap_height;
     x2 = x1;		bot y2 = 0;
penpos2(3rule_thickness,angle(z2-z3)+90);
penpos3(rule_thickness,270);
draw z1--z2;
penstroke z2e--z3e;
penlabels(1,2,3); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "Widerspruch";
beginchar(oct"032",8u#,body_height#,0);
adjust_fit(0,0);
pickup pencircle xscaled rule_thickness yscaled rule_thickness;
numeric c;      
c := 0.55;
    x1 = hround(w/2+u);                 top y1 = body_height;
    x4 = hround(w/2-u);                     y4 = 2u;
lft x2 = u;                                 y2 = c[y1,y4];
rt  x3 = w-u;                               y3 = c[y4,y1];
    z5 = z4+2u*dir(angle(z4-z3));    
    z6 = z5+dir(angle(z3-z5)+30)*3u; 
    z7 = z5+dir(angle(z3-z5)-30)*3u; 
draw z1--z2--z3--z5;
draw z7--z5--z6;                  
penlabels(1,2,3,4,5,6,7); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "Rechteck";
beginchar(oct"033",x_height#+2u#,x_height#,0);
adjust_fit(0,0);
pickup pencircle xscaled rule_thickness yscaled rule_thickness;
lft x1 = hround u;			bot y1 = 0;
    x2 = x1;				top y2 = x_height;
    x3 = w-x1;				    y3 = y2;
    x4 = x3;				    y4 = y1;
draw z1--z2--z3--z4--cycle;
penlabels(1,2,3,4); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "variant angle";
beginchar(oct"034",14u#,math_axis#+6u#,math_axis#-6u#);
adjust_fit(0,0);
pickup pencircle xscaled rule_thickness yscaled rule_thickness;
  lft x1 = hround u;          y1 = math_axis;
      x2 = w-x1;              y2 = .8[y5,y4];
      x3 = w-x1;              y3 = .8[y4,y6];
      x4 = .6428 *.6w+x1;     y4 =  .7660 *.6w+y1; % Radius: .6w
      x5 = .6w+x1;            y5 = y1;
      x6 = .6428 *.6w+x1;     y6 = -.7660 *.6w+y1;
draw z2--z1--z3;                             % Der Winkel
draw z4{(y4-y1)/w*right+(x4-x1)/w*down}.. z5{down}
  .. z6{(y6-y1)/w*right+(x6-x1)/w*down};     % Der Teilkreis
penlabels(1,2,3,4,5,6); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "weiblich";
beginchar(oct"035",x_height#+2u#,cap_height#,0);
numeric a;
adjust_fit(0,0);
pickup pencircle xscaled rule_thickness yscaled rule_thickness;
a := (x_height-rule_thickness)/2;
    x1 = good.x w/2;				top y1 = good.y (cap_height-a);
	z2 = z1+a*down;
    x3 = x1; 					top y3 = 0;
	x4 = x1+0.5*(y2-y3);			y4 = 0.5[y2,y3];
	x5 = 2[x4,x1];					y5 = y4;
draw fullcircle scaled (2*a) shifted z1;
draw z2--z3;
draw z4--z5;
penlabels(1,2,3,4,5); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "maennlich";
beginchar(oct"036",x_height#+3u#,cap_height#,0);
numeric c, b, a;
b:=70;
adjust_fit(0,0);
pickup pencircle xscaled rule_thickness yscaled rule_thickness;
a := (x_height-rule_thickness)/2;
    x1 = good.x w/2;			bot	y1 = good.y (a);
	z2 = z1 + dir(b)*a;
	z3 = c[z1,z2];				y3 = body_height;
	z4 = z3 - dir(b+30)*0.9*length(z3-z2);
	z5 = z3 - dir(b-30)*0.9*length(z3-z2);
draw fullcircle scaled (2*a) shifted z1;
draw z2--0.6[z2,z3];
%draw z3--z4;
%draw z3--z5;
fill z3--z5--0.5[z2,z3]--z4--cycle;
penlabels(1,2,3,4,5); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "Einheitskreis T";
beginchar("T",13u#-width_adj#+4.5u#,cap_height#,0);
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(0,0);
h:=vround(h-2stem_corr);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround(.5w-.5cap_stem-1.75u); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem

pos11(cap_stem,0); pos12(cap_stem,0);
lft x11l=lft x12l=hround (.5w-.5cap_stem+1.75u); top y11=h; bot y12=0;
filldraw stroke z11e--z12e; % stem
if serifs: 
fi;
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x1; rt x4r=hround(w-.65u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,.7beak_jut);  % right arm and beak
pos5(hair,180); x5=w-x4; y5=y4;
arm(3,5,f,beak_darkness,-.7beak_jut);  % left arm and beak
if serifs: 
  dish_serif( 2, 1,c,1/3,1.414cap_jut,d,1/3,1.414cap_jut);  % lower serif 
  dish_serif(12,11,k,1/3,1.414cap_jut,l,1/3,1.414cap_jut);  % lower serif
  nodish_serif( 1, 2,a,1/3,.5cap_jut,b,1/3,.5cap_jut);  % upper bracketing
  nodish_serif(11,12,m,1/3,.5cap_jut,n,1/3,.5cap_jut);  % upper serif
fi
math_fit(-.75cap_height#*slant,ic#-2.5u#); 
penlabels(1,2,3,4,5,6,11,12); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "ungerade Zahlen/odd numbers (U)";
beginchar("U",13u#+.5width_adj#+3.5u#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
if monospace: adjust_fit(cap_serif_fit#-.5u#,cap_serif_fit#-.5u#)
else: adjust_fit(cap_serif_fit#,cap_serif_fit#) fi;
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
pos2'(cap_stem,180); z2'=z2;
pos3(cap_band,-90);
pos4(cap_hair,0); pos5(cap_hair,0);
x1=x2; x3=.5[x1,x5]; x4=x5; x1l=w-x5r;
top y1=top y5=h; y2=y4=1/3h; bot y3r=-o;
lft x1l=hround max(2u,3u-.5cap_stem);
filldraw stroke z1e--z2e; % left stem
filldraw stroke pulled_arc.e(2',3)
 & pulled_arc.e(3,4)&z4e--z5e; % arc and right stem
% Zusatz für ganze Zahlen
pos6(cap_stem,0); pos7(1.11cap_stem,-30);
z6=z1+(3.3u,0u); x7=x6; y7=.02h;
filldraw stroke z6e--z7e; % middle stem
if serifs: dish_serif(1,2,a,1/3,cap_jut,b,1/3,cap_jut); % left serif
 dish_serif(6,7,e,1/3,cap_jut,f,1/3,cap_jut); % middle serif
 dish_serif(5,4,c,1/2,cap_jut,d,1/2,cap_jut)(dark); fi  % right serif
math_fit(-cap_serif_fit#-.3cap_height#*slant-min(cap_height#*slant,u#),
 max(.5ic#-.5u#,0)); penlabels(1,2,3,4,5,6,7); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "ganze Zahlen (Z)";
beginchar("Z",11u#+4u#,cap_height#,0);
italcorr cap_height#*slant-.5u#;
adjust_fit(0,0);
numeric arm_thickness[],z_stem;
if hefty: arm_thickness1=Vround(slab-vair_corr); arm_thickness2=slab;
 z_stem=.8[vair,cap_stem];
else: arm_thickness1=slab; arm_thickness2=vround(slab+vair_corr);
 z_stem=.9[vair,cap_stem]; fi
pickup tiny.nib;
x3l=x4l=w-x1r=w-x2r; lft x3l=hround u;
top y1=h; y2=min(y1,h-2/3arm_thickness1);
bot y4=0; y3=max(y4,2/3arm_thickness2);
x11l=x12l=x3l+4u; x9l=x10l=x1l-4u;
y9=y1; y10=y2; y11=y3; y12=y4;
numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l);
penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
penpos9(alpha*(z_stem-tiny),0);  penpos10(alpha*(z_stem-tiny),0);
penpos11(alpha*(z_stem-tiny),0); penpos12(alpha*(z_stem-tiny),0);
pair delta; delta=penoffset z3-z2 of currentpen;
fill top lft z9l--z10l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
 ---bot rt z4r--z3r-delta---z10r-delta..rt z10r---rt z9r..top z9r
 ---cycle;  % diagonal
fill top lft z1l--z2l+delta---z11l+delta..lft z11l---lft z12l..bot z12l
 ---bot rt z12r--z11r-delta---z2r-delta..rt z2r---rt z1r..top z1r
 ---cycle;  % diagonal
pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
top y5r=h; x5=x1; lft x6r=hround 1.25u; y6=good.y(y5l-beak)-eps;
arm(5,6,a,beak_darkness**.8,-.4beak_jut);  % upper arm and beak
pos7(arm_thickness2,-90); pos8(hair,0);
bot y7r=0; x7=x4; rt x8r=hround(w-.9u); y8=good.y(y7l+1.2beak)+eps;
arm(7,8,b,beak_darkness**.9,.6beak_jut);  % lower arm and beak
math_fit(0,.5ic#);
penlabels(1,2,3,4,5,6,7,8); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "real numbers (R)";
beginchar("R",if serifs: 12u#+.5max(2u#,cap_curve#)+4.5u#
 else:12.5u#-.5width_adj# fi,cap_height#,0);
italcorr .75cap_height#*slant- if serifs: 1.75 else: .5 fi\\ u#;
adjust_fit(cap_serif_fit#,0);
pickup tiny.nib;
pos1(cap_stem',0); pos2(cap_stem',0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem')+3.5u; top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem

%%%%%%%%  Zusatz für Mengen-Buchstaben:
%  für den zusätzlichen senkrechten Strich müssen die Zeichenbreite,
%  und die vom linken Rand abhängigen Größen um 4.5u bzw. 3.5u vergrößert
%  werden. Außerdem muß 'penlabels' angepaßt werden.
pos11(cap_stem,0); pos12(cap_stem,0);
lft x11l=lft x12l=hround max(2u,3u-.5cap_stem'); top y11=h; bot y12=0;
filldraw stroke z11e--z12e; % stem
% Trick, sonst wird der Strich zu dick:
penpos13(.5cap_band,30); penpos14(.5cap_band,30);
z13r=z1;z14r=z11;
filldraw stroke z13e{left}..{left}z14e;
penpos15(.5cap_band,30); penpos16(.5cap_band,30);
z15l= z2;z16l=z12;
filldraw stroke z15e--z16e;
if serifs: 
 nodish_serif(11,12,e,1/3,cap_jut,f,1/3,cap_jut);  % upper serif
 dish_serif(12,11,g,1/3,cap_jut,h,1/3,cap_jut);  % lower serif
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
penpos3(cap_band,90); penpos4(cap_band,90);
penpos5(cap_curve if hefty:-3stem_corr fi,0);
penpos6(vair,-90); penpos7(vair,-90);
z3r=top z1; y4=y3; y5=.5[y4l,y6l]; y6=y7;
x7=x2; y7l=vround(.5h+.5vair); x4=x6;
if serifs: x4=.5w-.5u+.5*4u; x5r=hround(w-2.25u);
else: x4=.5w+.5u; x5r=hround(w-u); fi
x4l:=x6l:=x4-.125cap_curve;
fill stroke z3e..pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;  % lobe
if serifs: pos6'(vair,-90); pos0(cap_stem,180);
 pos8(cap_curve,180); pos9(vair,-90); pos10(hair,0);
 z6'=z6; lft x0r=lft x8r=hround(x5-2/3u-.5cap_curve);
 y8=1/3[y2,y7]; y0=3/5[y2,y7]; x9=.5[x8l,x10r];
 bot y9r=-o; rt x10r=hround(w-.05u); y10=1/4[y2,y7];
 filldraw stroke z6'e{right}..z0e---z8e....z9e{right}..z10e{up};  % tail
 nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut);  % upper serif
 dish_serif(2,1,c,1/3,cap_jut,d,1/3,cap_jut);  % lower serif
else: penpos8(cap_stem-2stem_corr,0); penpos9(cap_stem,0);
 x8=x6+.5u; y8=y6; x9r=hround(w-.5u); y9=0;
 fill z8l--diag_end(8l,9l,.5,1,9r,8r)--z8r--cycle; fi  % tail
math_fit(0,.75ic#);
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "even numbers (E)";
beginchar("E",12u#-width_adj#+4.5u#,cap_height#,0);
italcorr cap_height#*slant-beak_jut#-.5u#;
adjust_fit(cap_serif_fit#,0);
h:=vround(h-stem_corr);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem)+3.5u; top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem

%%%%%%%%  Zusatz für Mengen-Buchstaben:
%  für den zusätzlichen senkrechten Strich müssen die Zeichenbreite,
%  und die vom linken Rand abhängigen Größen um 4.5u bzw. 3.5u vergrößert
%  werden. Außerdem muß 'penlabels' angepaßt werden (11,12,13,14,15,16).
pos11(cap_stem,0); pos12(cap_stem,0);
lft x11l=lft x12l=hround max(2u,3u-.5cap_stem'); top y11=h; bot y12=0;
filldraw stroke z11e--z12e; % stem
% Trick, sonst wird der Strich zu dick:
penpos13(.5cap_band,30); penpos14(.5cap_band,30);
z13r=z1;z14r=z11;
filldraw stroke z13e--z14e;
penpos15(.5cap_band,30); penpos16(.5cap_band,30);
z15l= z2;z16l=z12;
filldraw stroke z15e--z16e;
if serifs: 
 nodish_serif(11,12,i,1/3,cap_jut,j,1/3,cap_jut);  % upper serif
 dish_serif(12,11,k,1/3,cap_jut,l,1/3,cap_jut);  % lower serif
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x1; rt x4r=hround(w-u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,beak_jut);  % upper arm and beak
pos5(cap_bar,-90); pos6(hair,0);
top y5l=vround(.52[y2,y1]+.5cap_bar); x5=x1;
pos0(cap_bar,90); pos7(hair,0);
z0=z5; x6=x7; y6-y5l=y0l-y7;
if serifs: rt x6r=hround(w-4.4u+.5hair); y6=good.y(y5l+.6beak)+eps;
 rt x9r=hround(w-.5u);
else: rt x6r=hround(w-1.5u); y6=y5l+eps; rt x9r=hround(w-.75u); fi
arm(5,6,f,beak_darkness,0); arm(0,7,g,beak_darkness,0);  % middle arm and serif
pos8(slab if not serifs:+2stem_corr fi,-90); pos9(hair,0);
bot y8r=0; x8=x2; y9=good.y(y8l+7/6beak)+eps;
arm(8,9,h,beak_darkness,1.5beak_jut);  % lower arm and beak
if serifs: nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
math_fit(0,.5ic#);
penlabels(0,1,2,3,4,5,6,7,8,9,11,12,13,14,15,16); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
cmchar "Koerper K";
beginchar("K",13.5u#+4.5u#,cap_height#,0);
italcorr cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric right_jut,stem[],alpha[];
if serifs: right_jut=.6cap_jut;
else: right_jut=.4tiny; fi
pickup tiny.nib; pos1(fudged.cap_stem,0); pos2(fudged.cap_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5fudged.cap_stem)+3.5u; top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
%%%%%%%%  Zusatz für Mengen-Buchstaben:
%  für den zusätzlichen senkrechten Strich müssen die Zeichenbreite,
%  und die vom linken Rand abhängigen Größen um 4.5u bzw. 3.5u vergrößert
%  werden. Außerdem muß 'penlabels' angepaßt werden (11,12,13,14,15,16).
pos11(cap_stem,0); pos12(cap_stem,0);
lft x11l=lft x12l=hround max(2u,3u-.5cap_stem'); top y11=h; bot y12=0;
filldraw stroke z11e--z12e; % stem
% Trick, sonst wird der Strich zu dick:
penpos13(.5cap_band,30); penpos14(.5cap_band,30);
z13r=z1;z14r=z11;
filldraw stroke z13e--z14e;
penpos15(.5cap_band,30); penpos16(.5cap_band,30);
z15l= z2;z16l=z12;
filldraw stroke z15e--z16e;
if serifs: 
 nodish_serif(11,12,i,1/3,cap_jut,j,1/3,cap_jut);  % upper serif
 dish_serif(12,11,k,1/3,cap_jut,l,1/3,cap_jut);  % lower serif
fi;

stem2=max(tiny.breadth,fudged.cap_stem-3stem_corr);
stem1=max(tiny.breadth,fudged.hair if hefty:-3stem_corr fi);
top y3=h; rt x3r=hround(r-letter_fit-u-right_jut);
bot y6=0; rt x6r=hround(r-letter_fit-.75u-right_jut);
x4=x1; y4=1/3h;
alpha1=diag_ratio(1,.5(stem1-tiny),y3-y4,x3r-x4);
penpos3(alpha1*(stem1-tiny),0); penpos4(whatever,-90);
alpha2=diag_ratio(1,.5(stem2-tiny),y1-y6,x6r-x1);
penpos6(alpha2*(stem2-tiny),0);
forsuffixes $=l,r: y3'$=h; y6'$=0; z4$=z3'$+whatever*(z3-z4);
 z5$=z6'$+whatever*(z1-z6)=whatever[z3,z4]; endfor
z5=.5[z5l,z5r];
z3'r=z3r+penoffset z3-z4 of currentpen+whatever*(z3-z4);
% we have also |z3'l=z3l+penoffset z4-z3 of currentpen+whatever*(z3-z4)|;\]
z6'r=z6r+penoffset z1-z6 of currentpen+whatever*(z1-z6);
z6'l=z6l+penoffset z6-z1 of currentpen+whatever*(z1-z6);
fill z4r--diag_end(4r,3'r,1,.5,3'l,4l)--z4l--cycle;  % upper diagonal
fill z5l--diag_end(5l,6'l,.5,1,6'r,5r)--z5r--cycle;  % lower diagonal
if serifs: numeric inner_jut;
 if rt x2r+cap_jut+.5u+1<=lft x6l-cap_jut: inner_jut=cap_jut;
 else: rt x2r+cap_jut+.5u+1=lft x6l-inner_jut; fi
 dish_serif(1,2,a,1/3,cap_jut,b,1/3,cap_jut); % upper stem serif
 dish_serif(2,1,c,1/3,cap_jut,d,1/3,cap_jut);  % lower stem serif
 dish_serif(3,4,e,2/3,1.2cap_jut,f,1/2,right_jut)(dark); % upper diagonal serif
 dish_serif(6,5,g,1/2,inner_jut,h,1/3,right_jut)(dark);fi % lower diagonal serif
math_fit(0,.5ic#); penlabels(1,2,3,4,5,6,11,12,13,14,15,16); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "natural Numbers N";
beginchar("N",13u#+width_adj#+4.5u#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric thin_stem; thin_stem=hround(fudged.hair+stem_corr);
pickup tiny.nib; pos1(thin_stem,0); pos2(thin_stem,0);
pos3(thin_stem,0); pos4(thin_stem,0);
pickup tiny.nib; top y1=top y3=h; bot y2=bot y4=0;
x1=x2; x3=x4; x1l=w-x3+3.5u;
rt x3r=hround min(w-2u,w-3u+.5fudged.cap_stem);
filldraw stroke z1e--z2e; % left stem
filldraw stroke z3e--z4e; % right stem

%%%%%%%%  Zusatz für Mengen-Buchstaben:
%  für den zusätzlichen senkrechten Strich müssen die Zeichenbreite,
%  und die vom linken Rand abhängigen Größen um 4.5u bzw. 3.5u vergrößert
%  werden. Außerdem muß 'penlabels' angepaßt werden (11,12,13,14,15,16).
pos11(cap_stem,0); pos12(cap_stem,0);
lft x11l=lft x12l=hround max(2u,3u-.5cap_stem'); top y11=h; bot y12=0;
filldraw stroke z11e--z12e; % stem
% Trick, sonst wird der Strich zu dick:
penpos13(.5cap_band,30); penpos14(.5cap_band,30);
z13r=z1;z14r=z11;
filldraw stroke z13e--z14e;
penpos15(.5cap_band,30); penpos16(.5cap_band,30);
z15l= z2;z16l=z12;
filldraw stroke z15e--z16e;
if serifs: 
 nodish_serif(11,12,i,1/3,cap_jut,j,1/3,cap_jut);  % upper serif
 dish_serif(12,11,k,1/3,cap_jut,l,1/3,cap_jut);  % lower serif
fi;

if hefty: penpos5(fudged.cap_stem,0); penpos6(fudged.cap_stem,0);
 x5l=x1; x6r=x4; y5=h; y6=0;
 numeric upper_notch,lower_notch;
 upper_notch=h-cap_notch_cut; lower_notch=cap_notch_cut;
 x1'=rt x1r; z1'=whatever[z5l,z6l]; x4'=lft x4l; z4'=whatever[z5r,z6r];
 fill z5l..
  if y1'<upper_notch: {right}(x1'+1,upper_notch){down}... fi
  {z6-z5}diag_in(5l,6l,1,6r)--z6r..
  if y4'>lower_notch: {left}(x4'-1,lower_notch){up}... fi
  {z5-z6}diag_in(6r,5r,1,5l)--cycle;  % diagonal
else: penpos5(whatever,0); penpos6(whatever,90);
 z5l=z1l; z6l=z4l;
 z7=z6l+(max(eps,cap_stem-3stem_corr-tiny),0) rotated (angle(z5l-z6l)-90);
 z5r=z7+whatever*(z5l-z6l); z6r=z7+whatever*(z5l-z6l);
 filldraw stroke z5e..z6e; fi  % diagonal
if serifs: if hefty: serif(1,2,a,1/3,-cap_jut); % upper left serif
 else: serif(5,6,a,1/3,-cap_jut); fi  % upper left serif
 dish_serif(2,1,b,1/2,cap_jut,c,1/2,cap_jut)(dark); % lower left serif
 dish_serif(3,4,e,1/2,cap_jut,f,1/2,cap_jut)(dark); fi  % upper right serif
math_fit(0,max(.5ic#-.5u#,0)); 
penlabels(1,1',2,3,4,4',5,6,7,11,12,13,14,15,16); 
endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "complex numbers (C)";
if serifs: beginchar("C",13u#+1u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(cap_hair,0); pos2(cap_band,90);
 pos3(cap_curve,180); pos4(cap_band,270); pos5(hair,360);
 rt x1r=rt x5r=hround(w-u); lft x3r=hround u; x2=x4=.55[x3,x1];
 top y2r=h+o; bot y4r=-o; y3=.5[y2,y4];
 bot y1=min(vround max(.6h,x_height-.5vair),bot y2l-eps);
 y5=max(good.y .95(h-y1),y4l+eps);
 (x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
 (x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
 filldraw stroke z1e{x2-x1,10(y2-y1)}
  ...pulled_arc.e(2,3) & pulled_arc.e(3,4)...{up}z5e; % arc
 pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=h+o;
 x1r-x1'=2cap_curve-fine; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
 % Zusatz für complexe Zahlen:
 pickup tiny.nib;
 pos7(1.11cap_stem,30); pos8(1.11cap_stem,-30);
 x7=x8=.33[x3,x1]; y7=.96h; y8=.04h;
 filldraw stroke z7e--z8e;
else: beginchar("C",11.5u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
 pos3(cap_curve,180); pos4(slab,270); pos5(flare,275);
 rt x1r=hround(w-1.1u); x2=x4=.5w+1.25u;
 lft x3r=hround max(u,2u-.5cap_curve); rt x5r=hround(w-.9u);
 top y1r=vround .95h+o; top y2r=h+o; y3=.5h;
 bot y4r=-o; bot y5r=vround .08h-o; y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke rterm.e(2,1,right,.9,4) & super_arc.e(2,3)
  & super_arc.e(3,4) & term.e(4,5,right,.8,4); fi % arc and terminals
math_fit(-.3cap_height#*slant-.5u#,.5ic#);
penlabels(1,1',2,3,4,5,6,7,8); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "even numbers/gerade Zahlen (G)";
if serifs: beginchar("G",14u#+1u#,cap_height#,0);
 italcorr cap_height#*slant-1.5u#-.5cap_serif_fit#;
 adjust_fit(0,.5cap_serif_fit#);
 pickup tiny.nib; pos0(cap_stem,0); pos7(cap_stem,0);
 rt x0r=hround(w-2u); y0=good.y(.1[bar_height,x_height]+1); x7=x0;
 pickup fine.nib;
 if hefty: bot y7=0; pos5(cap_stem,0); x5=x0; top y5=tiny.top y0;
 else: pos5(cap_hair,0); pos8(cap_stem,0);
  y7=.5bar_height; z5l=z8l; z8=z7; x9=x8r; bot y9=0;
  filldraw z8l{down}..{4(x9-x8),y9-y8}z9--z8r--cycle; fi % spur
 pos1(cap_hair,0); pos2(cap_band,90);
 pos3(cap_curve,180); pos4(cap_band,270);
 rt x1r=hround(w-2u); lft x3r=hround u; x2=x4=.55[x3,x1];
 top y2r=h+o; bot y4r=-o; y3=.5[y2,y4];
 bot y1=min(vround max(.6h,x_height-.5vair),bot y2l-eps);
 (x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
 (x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
 filldraw stroke z1e{x2-x1,10(y2-y1)}
  ...pulled_arc.e(2,3) & pulled_arc.e(3,4)...z5e{up}; % arc
 pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=h+o;
 x1r-x1'=2cap_curve-fine; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
 pickup tiny.nib; filldraw stroke z0e--z7e;  % stem
 dish_serif(0,7,a,1/3,max(cap_jut,2.25u),b,1/3,1.25u);  % serif
 math_fit(-.3cap_height#*slant-.5u#,ic#);
 % Zusatz für gerade Zahlen:
 pos10(1.11cap_stem,30); pos11(1.11cap_stem,-30);
 x10=x11=.33[x3,x1]; y10=.96h; y11=.04h;
 filldraw stroke z10e--z11e;
else: beginchar("G",12u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
 pos3(cap_curve,180); pos4(slab,270); pos5(flare,270);
 rt x1r=hround(w-1.35u); x2=x4=.5w+u;
 lft x3r=hround max(u,2u-.5cap_curve); rt x5l=hround(w-1.2u);
 top y1r=vround .93h+o; top y2r=h+o; y3=.5h;
 bot y4r=-o; bot y5r=vround .07h-o;
 filldraw stroke rterm.e(2,1,right,.9,4)&super_arc.e(2,3)
  & super_arc.e(3,4) & term.e(4,5,right,1,4); % arc and terminals
 pos0(stem,0); pos7(stem,0);
 z7r=z5r; x0=x7; top y0=1+vround .35[bar_height,x_height];
 filldraw stroke z0e--z7e;  % stem
 pos8(cap_bar,90); pos9(cap_bar,90);
 z0r=z9r; y8=y9; lft x8=hround x4;
 filldraw stroke z8e--z9e;  % bar
 math_fit(-.3cap_height#*slant-.5u#,.5ic#); fi
penlabels(0,1,1',2,3,4,5,6,7,8,9,10,11); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "odd numbers (O)";
beginchar("O",14u#-width_adj#+1u#,cap_height#,0);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
penpos1(vair',90); penpos3(vround(vair+1.5vair_corr),-90);
penpos2(cap_curve,180); penpos4(cap_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=sqrt superness;  % make |"O"|, not |"0"|
else: x2r=hround u; fi
x4r=w-x2r; x1=x3=.5w; y1r=h+o; y3r=-o;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
% Zusatz für ungerade Zahlen:
pickup tiny.nib;
pos5(1.11cap_stem,30); pos6(1.11cap_stem,-30);
x5=x6=.33[x2,x4]; y5=.96h; y6=.04h;
filldraw stroke z5e--z6e;
math_fit(-.3cap_height#*slant-.5u#,ic#-.5u#); penlabels(1,2,3,4,5,6); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmchar "rationale Zahlen (Q)";
beginchar("Q",14u#-width_adj#+1.5u#,cap_height#,comma_depth#);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
numeric light_curve; light_curve=cap_curve-hround stem_corr;
penpos1(vair',90); penpos3(vair',-90);
penpos2(light_curve,180); penpos4(light_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=sqrt superness;  % make |"Q"| like |"O"|
else: x2r=hround u; fi
x4r=w-x2r; x1=x3=.5w; y1r=h+o; y2=y4=.5h-vair_corr; y3r=-o;
penstroke pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
pickup tiny.nib;
if hefty: penpos5(1.2(.5[cap_bar,light_curve]),0); penpos6(1.2light_curve,0);
 x5=.5w; x6r=hround(w-1.5u);
 y5=vround .28h; y6=-d;
 fill diag_end(6r,5r,.25,1,5l,6l)--diag_end(5l,6l,.5,1,6r,5r)--cycle;  % tail
else: pos3'(vair,270); pos5(vair,180); pos6(vair,90);
 pos7(cap_curve,85); penpos8(eps,180);
 z3'=z3; x6=x3; top y6r=vround(.2h+.5vair);
 lft x5r=hround(.5w-1.25u-.5vair); y5=.5[y3,y6];
 bot y7l=-d; x7l=2/3[x6,x8]; y8=0; rt x8=hround(x4r+.1u);
 filldraw stroke z3'e{left}...z5e{up}...z6e{right}
  ..z7e{right}...z8e{up}; fi % tail
% Zusatz für rationale Zahlen:
pos9(1.11cap_stem,25); pos10(1.11cap_stem,-25);
x9=x10=.34[x2,x4]; y9=.98h; y10=.02h;
filldraw stroke z9e--z10e;
math_fit(-.3cap_height#*slant-.5u#,ic#);
penlabels(1,2,3,4,5,6,7,8,9,10); endchar;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


